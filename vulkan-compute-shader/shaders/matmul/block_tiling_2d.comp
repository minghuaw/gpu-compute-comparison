#version 460
#extension GL_EXT_control_flow_attributes : enable

const uint M = 4096;
const uint N = 4096;
const uint K = 4096;
const uint BM = 64;
const uint BN = 64;
const uint BK = 32;
const uint TM = 8; // Must be powers of 2
const uint TN = 4; // TN must be 4 for manual vectorization with vec4

const uint VN = N / 4; // number of vec4 in N
const uint VBN = BN / 4; // number of vec4 in BN

const float alpha = 1.0;
const float beta = 1.0;

layout(local_size_x = BM/TM, local_size_y = BN/TN, local_size_z = 1) in;

layout(set = 0, binding = 0) buffer readonly MatrixA {
    float a_global[];
};

layout(set = 0, binding = 1) buffer readonly MatrixB {
    float b_global[];
};

layout(set = 0, binding = 2) buffer MatrixC {
    vec4 c_global[]; // use vec4 for manual vectorization
};

shared float a_shared[BM * BK];
shared float b_shared[BK * BN];

void main() {
    uint thread_index = gl_LocalInvocationIndex;
    uint thread_num = (BN / TN) * (BM / TM);
    
    uint ty = (thread_index / (BN / TN)) * TM;
    uint tx = (thread_index % (BN / TN)) * TN;

    uint a_global_offset = gl_WorkGroupID.y * BM * K;
    uint b_global_offset = gl_WorkGroupID.x * BN;
    const uint global_offset = gl_WorkGroupID.y * BM * VN + gl_WorkGroupID.x * VBN;

    uint a_tile_row = thread_index / BK;
    uint a_tile_col = thread_index % BK;
    uint a_tile_stride = thread_num / BK;

    uint b_tile_row = thread_index / BN;
    uint b_tile_col = thread_index % BN;
    uint b_tile_stride = thread_num / BN;

    float c_tile[TM * TN];
    [[unroll]] for (uint i = 0; i < TM * TN; i++) {
        c_tile[i] = 0.0;
    }

    for (uint k=0; k<K; k+=BK) {
        for (uint i = 0; i < BM; i += a_tile_stride) {
            a_shared[(a_tile_row + i) * BK + a_tile_col] = a_global[a_global_offset + (a_tile_row + i) * K + a_tile_col];
        }
        for (uint i = 0; i < BK; i += b_tile_stride) {
            b_shared[(b_tile_row + i) * BN + b_tile_col] = b_global[b_global_offset + (b_tile_row + i) * N + b_tile_col];
        }

        barrier();
        a_global_offset += BK;
        b_global_offset += BK * N;

        [[unroll]] for (uint kk=0; kk<BK; kk++) {
            [[unroll]] for (uint i=0; i<TM; i++) {
                [[unroll]] for (uint j=0; j<TN; j++) {
                    c_tile[i*TN+j] += a_shared[(ty + i) * BK + kk] * b_shared[kk * BN + tx + j];
                }
            }
        }
        barrier();
    }

    uint thread_row = ty;
    uint thread_col = tx / 4;
    [[unroll]] for (uint i = 0; i < TM; i++) {
        // Manual vectorization
        vec4 v = vec4(c_tile[i*TN + 0], c_tile[i*TN + 1], c_tile[i*TN + 2], c_tile[i*TN + 3]);
        c_global[global_offset + (thread_row + i) * VN + (thread_col + 0)] = v;
    }
}