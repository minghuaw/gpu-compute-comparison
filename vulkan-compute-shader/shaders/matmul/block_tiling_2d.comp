#version 460

const uint M = 4096;
const uint N = 4096;
const uint K = 4096;
const uint BM = 64;
const uint BN = 64;
const uint BK = 32;
const uint TM = 8;
const uint TN = 1; // Performance is significantly worse when TN is not 1.
                   // The performance cost mainly comes from writing back to global C.

const float alpha = 1.0;
const float beta = 1.0;

layout(local_size_x = BM/TM, local_size_y = BN/TN, local_size_z = 1) in;

layout(set = 0, binding = 0) buffer MatrixC {
    float c_global[];
};

layout(set = 0, binding = 1) buffer readonly MatrixA {
    float a_global[];
};

layout(set = 0, binding = 2) buffer readonly MatrixB {
    float b_global[];
};

shared float a_shared[BM * BK];
shared float b_shared[BK * BN];

void main() {
    // This is just to make sure the buffers are not optimized away
    a_shared[0] = a_global[0];
    b_shared[0] = b_global[0];
    c_global[0] = a_shared[0] + b_shared[0];

    uint thread_index = gl_LocalInvocationIndex;

    uint thread_num = (BN / TN) * (BM / TM);
    uint tx = (thread_index % (BN / TN)) * TN;
    uint ty = (thread_index / (BN / TN)) * TM;

    uint a_global_offset = gl_WorkGroupID.y * BM * K;
    uint b_global_offset = gl_WorkGroupID.x * BN;
    const uint c_global_offset = gl_WorkGroupID.y * BM * N + gl_WorkGroupID.x * BN;

    uint a_tile_row = thread_index / BK;
    uint a_tile_col = thread_index % BK;
    uint a_tile_stride = thread_num / BK;

    uint b_tile_row = thread_index / BN;
    uint b_tile_col = thread_index % BN;
    uint b_tile_stride = thread_num / BN;

    float c_tile[TM * TN];
    for (uint i = 0; i < TM * TN; i++) {
        c_tile[i] = 0.0;
    }

    for (uint k=0; k<K; k+=BK) {
        for (uint i = 0; i < BM; i += a_tile_stride) {
            a_shared[(a_tile_row + i) * BK + a_tile_col] = a_global[a_global_offset + (a_tile_row + i) * K + a_tile_col];
        }
        for (uint i = 0; i < BK; i += b_tile_stride) {
            b_shared[(b_tile_row + i) * BN + b_tile_col] = b_global[b_global_offset + (b_tile_row + i) * N + b_tile_col];
        }

        barrier();
        a_global_offset += BK;
        b_global_offset += BK * N;

        for (uint kk=0; kk<BK; kk++) {
            for (uint i=0; i<TM; i++) {
                for (uint j=0; j<TN; j++) {
                    c_tile[i*TN+j] += a_shared[(ty + i) * BK + kk] * b_shared[kk * BN + tx + j];
                }
            }
        }
        barrier();
    }

    // TODO: Writing to C is slow when TN is not 1.
    for (uint i=0; i<TM; i++) {
        for (uint j=0; j<TN; j++) {
            c_global[c_global_offset + (ty + i) * N + tx + j] = alpha * c_tile[i*TN+j] + beta * c_global[c_global_offset + (ty + i) * N + tx + j];
        }
    }
    // for (uint i=0; i<TM; i++) {
    //     c_global[c_global_offset + (ty + i) * N + tx + 0] = alpha * c_tile[i*TN+0] + beta * c_global[c_global_offset + (ty + i) * N + tx + 0];
    //     c_global[c_global_offset + (ty + i) * N + tx + 1] = alpha * c_tile[i*TN+1] + beta * c_global[c_global_offset + (ty + i) * N + tx + 1];
    // }
}