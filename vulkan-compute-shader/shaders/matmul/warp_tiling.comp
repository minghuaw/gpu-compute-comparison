#version 460
#extension GL_EXT_control_flow_attributes: enable
#extension GL_KHR_shader_subgroup_basic: enable

const uint M = 4096;
const uint N = 4096;
const uint K = 4096;
const uint BM = 128;
const uint BN = 128;
const uint BK = 32;
const uint WM = 64;
const uint WN = 64;
const uint TM = 8; // Must be powers of 2
const uint TN = 4; // TN must be 4 for manual vectorization with vec4

const uint VN = N / 4; // number of vec4 in N
const uint VBN = BN / 4; // number of vec4 in BN
const uint VK = K / 4; // number of vec4 in K
const uint VBK = BK / 4; // number of vec4 in BK

const uint SWM = 32; // Size of sub-warp tile in M
const uint SWN = 16; // Size of sub-warp tile in N

const float alpha = 1.0;
const float beta = 1.0;

layout(local_size_x = BM/TM, local_size_y = BN/TN, local_size_z = 1) in;

layout(set = 0, binding = 0) buffer readonly MatrixA {
    vec4 a_global[];
};

layout(set = 0, binding = 1) buffer readonly MatrixB {
    vec4 b_global[];
};

layout(set = 0, binding = 2) buffer MatrixC {
    vec4 c_global[]; // use vec4 for manual vectorization
};

shared float a_shared[BM * BK];
shared float b_shared[BK * BN];

void main() {
    uint thread_index = gl_LocalInvocationIndex;
    uint thread_num = (BN / TN) * (BM / TM);

    // Vulkan has built-in variable for subgroup/warp id
    uint subgroup_size = gl_SubgroupSize;
    uint subgroup_id = gl_SubgroupID;
    uint subgroup_col = subgroup_id % (BN / WN);
    uint subgroup_row = subgroup_id / (BN / WN);
    
    uint ty = (thread_index / (BN / TN)) * TM;
    uint tx = (thread_index % (BN / TN)) * TN;

    uint a_global_offset = gl_WorkGroupID.y * BM * VK;
    uint b_global_offset = gl_WorkGroupID.x * VBN;
    const uint global_offset = gl_WorkGroupID.y * BM * VN + gl_WorkGroupID.x * VBN;

    uint a_shared_row = thread_index / VBK;
    uint a_shared_col = thread_index % VBK;
    uint a_shared_stride = thread_num / VBK;

    uint b_shared_row = thread_index / VBN;
    uint b_shared_col = thread_index % VBN;
    uint b_shared_stride = thread_num / VBN;

    float a_tile[TM];
    [[unroll]] for (uint i = 0; i < TM; i++) {
        a_tile[i] = 0.0;
    }

    float b_tile[TN];
    [[unroll]] for (uint i = 0; i < TN; i++) {
        b_tile[i] = 0.0;
    }

    float c_tile[TM * TN];
    [[unroll]] for (uint i = 0; i < TM * TN; i++) {
        c_tile[i] = 0.0;
    }

    for (uint k=0; k<K; k+=BK) {
        for (uint i = 0; i < BM; i += a_shared_stride) {
            vec4 tmp = a_global[a_global_offset + (a_shared_row + i) * VK + a_shared_col];
            uint shared_index = (a_shared_row + i) * BK + a_shared_col * 4;
            a_shared[shared_index + 0] = tmp.x;
            a_shared[shared_index + 1] = tmp.y;
            a_shared[shared_index + 2] = tmp.z;
            a_shared[shared_index + 3] = tmp.w;
        }
        for (uint i = 0; i < BK; i += b_shared_stride) {
            vec4 tmp = b_global[b_global_offset + (b_shared_row + i) * VN + b_shared_col];
            uint shared_index = (b_shared_row + i) * BN + b_shared_col * 4;
            b_shared[shared_index + 0] = tmp.x;
            b_shared[shared_index + 1] = tmp.y;
            b_shared[shared_index + 2] = tmp.z;
            b_shared[shared_index + 3] = tmp.w;
        }

        barrier();
        a_global_offset += VBK;
        b_global_offset += BK * VN;

        [[unroll]] for (uint kk=0; kk<BK; kk++) {
            [[unroll]] for (uint i=0; i<TM; i+=4) {
                // a_tile[i] = a_shared[(ty + i) * BK + kk];
                vec4 tmp = vec4(a_shared[(ty + i) * BK + kk], a_shared[(ty + i + 1) * BK + kk], a_shared[(ty + i + 2) * BK + kk], a_shared[(ty + i + 3) * BK + kk]);
                a_tile[i] = tmp.x;
                a_tile[i + 1] = tmp.y;
                a_tile[i + 2] = tmp.z;
                a_tile[i + 3] = tmp.w;
            }
            [[unroll]] for (uint i=0; i<TN; i+=4) {
                // b_tile[i] = b_shared[kk * BN + tx + i];
                vec4 tmp = vec4(b_shared[kk * BN + tx + i], b_shared[kk * BN + tx + i + 1], b_shared[kk * BN + tx + i + 2], b_shared[kk * BN + tx + i + 3]);
                b_tile[i] = tmp.x;
                b_tile[i + 1] = tmp.y;
                b_tile[i + 2] = tmp.z;
                b_tile[i + 3] = tmp.w;
            }

            [[unroll]] for (uint i=0; i<TM; i++) {
                [[unroll]] for (uint j=0; j<TN; j++) {
                    // c_tile[i*TN+j] += a_shared[(ty + i) * BK + kk] * b_shared[kk * BN + tx + j];
                    c_tile[i*TN+j] += a_tile[i] * b_tile[j];
                }
            }
        }
        barrier();
    }

    uint thread_row = ty;
    uint thread_col = tx / 4;
    [[unroll]] for (uint i = 0; i < TM; i++) {
        // Manual vectorization
        vec4 v = vec4(c_tile[i*TN + 0], c_tile[i*TN + 1], c_tile[i*TN + 2], c_tile[i*TN + 3]);
        c_global[global_offset + (thread_row + i) * VN + (thread_col + 0)] = v;
    }
}